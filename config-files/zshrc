HISTFILE=~/.zsh_history
HISTSIZE=120000
SAVEHIST=100000
setopt HIST_EXPIRE_DUPS_FIRST    # Expire a duplicate event first when trimming history.
setopt HIST_FIND_NO_DUPS         # Do not display a previously found event.
setopt HIST_IGNORE_ALL_DUPS      # Delete an old recorded event if a new event is a duplicate.
setopt HIST_IGNORE_DUPS          # Do not record an event that was just recorded again.
setopt HIST_IGNORE_SPACE         # Do not record an event starting with a space.
setopt HIST_SAVE_NO_DUPS         # Do not write a duplicate event to the history file.
setopt SHARE_HISTORY             # Share history between all sessions.

. ~/.zsh/zsh-autosuggestions/zsh-autosuggestions.zsh
. ~/.zsh/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

function zvm_config() {
	ZVM_LINE_INIT_MODE=$ZVM_MODE_INSERT
	ZVM_INSERT_MODE_CURSOR=$ZVM_CURSOR_BLINKING_BLOCK
	ZVM_NORMAL_MODE_CURSOR=$ZVM_CURSOR_BLOCK
	ZVM_OPPEND_MODE_CURSOR=$ZVM_CURSOR_UNDERLINE
}

function zvm_after_init() {
	. ~/.my/aliases.sh
	. ~/.my/zsh/plugins/harpoon_paths.zsh
	. ~/.my/zsh/plugins/harpoon_zsh.zsh
	. ~/.my/zsh/bindkeys.zsh
}

. ~/.zsh/zsh-vi-mode/zsh-vi-mode.plugin.zsh

additional_paths=(
	~/.local/bin
	/mnt/d/wsl/path
	/opt/mssql-tools18/bin
)
for p in "${additional_paths[@]}"
do
	if [[ -d "$p" && ":$PATH:" != *":$p:"* ]] ; then
		PATH="$PATH:$p"
	fi
done
unset additional_paths

additional_sources=(
	~/foobar/barfoo.sh
)
for s in "${additional_sources[@]}"
do
	if [ -f "$s" ] ; then
		. "$s"
	fi
done
unset additional_sources

if [ -f ~/.local/bin/mise ]; then
	eval "$(~/.local/bin/mise activate zsh)"
fi

export COLORTERM="truecolor"
export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=#7A7A7A"

custom_prompt() {
	local git_branch=""
	if command -v git >/dev/null 2>&1; then
		git_branch=$(timeout 2s git branch --show-current 2>/dev/null)
		if [[ -z $git_branch ]]; then
			git_branch=$(timeout 2s git rev-parse --short HEAD 2>/dev/null)
			if [[ -n $git_branch ]]; then
				git_branch="@$git_branch"
			fi
		fi
		if [[ -n $git_branch ]]; then
			git_branch=" %F{yellow}($git_branch)%f"
		fi
	fi
	echo "%2~${git_branch} > "
}
setopt PROMPT_SUBST
PROMPT='$(custom_prompt)'
